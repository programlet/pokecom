10 ' [ 5GAMES IN 1 ]
20 '
30 CLEAR :RANDOMIZE :DIM G$(4),H(4)
40 FOR I=0TO 4:READ G$(I):NEXT :OPEN "E:5IN1H.DAT"FOR INPUT AS #1:INPUT #1,H(*):CLOSE
50 CLS :LINE (2,2)-(237,29),B:LOCATE 1,0:PRINT " 5GAMES IN 1  SELECT GAME ! ":LOCATE 24,3:PRINT " (C)MINDELIGHT "
60 LOCATE 2,2:PRINT USING "No# &&&&&&&&&&&&&&&&";G+1;G$(G):GOSUB 360:IF V>0AND V<6LET G=V-1
70 IF A<>32GOTO 60
80 X=14-LEN G$(G)/2:CLS :FOR I=0TO X:LOCATE 5+I,1:PRINT " ";G$(G):NEXT
90 LOCATE 17,3:PRINT USING "BEST #";H(G)+1:LINE (54,4)-(185,18),B
100 GOSUB 360:S=0:ON G GOTO 140,170,200,240
110 GOSUB 370:X=1,Y=15:LINE (0,S*1.4)-(239,31-S*1.4),B
120 WHILE POINT (X,Y)=0:PSET (X,Y):X=X+1,Y=Y+1+(INKEY$ <>"")*2:WEND
130 C=-(X=239):ON C+1GOSUB 410,420:S=S+C:GOTO 110
140 GOSUB 370:GOSUB 430:X=1,Y=15,M=0
150 WHILE POINT (X,Y)=0:PSET (X,Y):X=X+1,Y=Y+M,M=M+0.07+(INKEY$ <>"")*0.14:WEND
160 C=-(X=240):ON C+1GOSUB 410,420:S=S+C:GOTO 140
170 GOSUB 370:GOSUB 430:X=1,Y=15,M=0
180 WHILE POINT (X,Y)=0:PSET (X,Y):IF INKEY$ WHILE INKEY$ :WEND :M=-M
190 X=X+1,Y=Y+M,M=M+0.1:WEND :C=-(X=240):ON C+1GOSUB 410,420:S=S+C:GOTO 170
200 GOSUB 370:L=7+S,H=300+S*110,P=0
210 FOR Q=0TO L-1:CLS :K$="",R=150-S*8,T$=CHR$ (64+RND 26):X=RND 40-1,Y=RND 3:LOCATE X,Y:PRINT T$
220 WHILE K$<>T$ AND R>1:LOCATE 0,0:PRINT "CLEAR";H;" REST";L-Q;" SCORE";P;"+";R:R=R-1,K$=INKEY$ :WEND
230 P=P+R,X=X*6+2,Y=Y*8+3:GOSUB 410:NEXT :C=-(P>H),S=S+C:ON C GOSUB 420:GOTO 200
240 GOSUB 370:X=0,T=0,R=0,A=0
250 LINE (0,31)-(239,31):LINE (0,0)-(7,30),&1305,BF
260 GCURSOR (8,30):GPRINT "0205FE":GCURSOR (236,30):GPRINT "02FD02"
270 LOCATE 17,0:PAUSE "START!":LOCATE 17,0:PRINT "      "
280 IF R<1LET M=32-S^2/2+RND 2
290 R=R+1:IF R<M GOTO 320
300 LOCATE 10+T+T,0:PRINT MID$ ("ÀÞÙ Ï » Ý ¶Þº Û Ý ÀÞ",T+T+1,2):T=T+1,R=0
310 IF T>9GOSUB 460:T=0:IF A LET C=0:GOTO 340
320 IF INKEY$ LET A=-(A=0),X=X+1:GCURSOR (236-X,30):GPRINT MID$ ("02FD02E41AE4",A*6+1,6);"00":IF X>221LET C=1:GOTO 340
330 GOTO 280
340 GOSUB 460:LOCATE 16,0:PAUSE "YOU ";MID$ ("LOSEWIN!",C*4+1,4):S=S+C:GOTO 240
350 DATA SANDWICH CHILING,TRAVEL UFO,JUMP STREET,KEY HUNTING,DARUMA SLIPS
360 WHILE INKEY$ :WEND :K$="":WHILE K$="":K$=INKEY$ :WEND :A=ASC K$,V=VAL K$:RETURN
370 CLS :IF S>8GOTO 470
380 LINE (0,11)-(239,11),&H5555:LOCATE 16,1:PRINT USING " LEVEL # ";S+1:IF S<=H(G)GOTO 400
390 LOCATE 13,3:PRINT "BEST PLAYING !!":H(G)=S:OPEN "E:5IN1H.DAT"FOR OUTPUT AS #1:FOR I=0TO 4:PRINT #1,H(I):NEXT :CLOSE
400 GOSUB 360:IF A=9GOTO 50ELSE CLS :RETURN
410 FOR I=0TO 3:FOR J=0TO 5:LINE (X-J,Y-J)-(X+J,Y+J),X,B:NEXT :NEXT :RETURN
420 CLS :LINE (83,5)-(155,17),&H5A5A,B:LOCATE 17,1:PRINT "CLEAR!":WHILE INKEY$ ="":LINE (83,5)-(155,17),X,B:WEND :RETURN
430 FOR I=0TO 33:IF RND 14>S+2GOTO 450
440 LOCATE 3+I,RND 4-1:PRINT CHR$ (RND 215+32)
450 NEXT :LINE (0,0)-(239,31),B:LINE (239,2+S*1.4)-(239,29-S*1.4),R:RETURN
460 LOCATE 10,0:FOR Q=0TO 19:FOR W=0TO 9:NEXT :PRINT " ";:NEXT :RETURN
470 C$=G$(G)+" LEVEL 9 CLEAR!!  CONGRATULATIONS!!":FOR I=0TO 15:C$=" "+C$+" ":NEXT :L=LEN C$
480 LINE (2,2)-(237,29),&H5A5A,B:LOCATE 8,0:PAUSE " THANK YOU FOR PLAYING "
490 X=0:WHILE INKEY$ ="":LOCATE 11,2:PRINT MID$ (C$,X+1,17):FOR W=0TO 59:NEXT :X=(X+1)*-(X<L-15):WEND :GOTO 50
500 OPEN "E:5IN1H.DAT"FOR OUTPUT AS #1:PRINT #1,0,0,0,0,0:CLOSE :END
